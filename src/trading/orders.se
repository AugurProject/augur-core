# Copyright (C) 2015 Forecast Foundation OU, full GPL notice in LICENSE

#### must be first data members to match Delegator.sol
data controller
data controllerLookupName
####

macro ORDERS_FETCHER: self.controller.lookup('ordersFetcher')

inset('../macros/externs.sem')
inset('../macros/require.sem')
inset('../macros/safeMath.sem')
inset('../macros/assertNoValue.sem')
inset('../macros/assertPrivateCall.sem')
inset('../macros/orderFields.sem')

### Storage of all data associated with orders

data owner
data initialized

# indices: marketID, outcomeID, type, orderID
data orders[][][][](fxpAmount, fxpPrice, owner, fxpSharesEscrowed, fxpMoneyEscrowed, betterOrderID, worseOrderID, gasPrice)
# prices keeps track of the latest traded fxpPrice for a given outcome in a market [market is the key]
data marketOrderData[](volume, prices[])
# indices are marketID, outcomeID, and type
data bestOrder[][][]
data worstOrder[][][]

event CancelOrder(market: address: indexed, sender: address: indexed, fxpPrice: int256, fxpAmount, orderID: address, outcome: int256, type: int256, cashRefund: int256, sharesRefund: int256)
event CompleteSets(sender: address: indexed, market: address: indexed, type: indexed, fxpAmount: int256, numOutcomes: int256, marketCreatorFee: int256, reportingFee: int256)
event MakeOrder(market: address: indexed, sender: address: indexed, type, fxpPrice: int256, fxpAmount: int256, outcome: int256, orderID: address, fxpMoneyEscrowed: int256, fxpSharesEscrowed: int256, tradeGroupID: int256)
event TakeOrder(market: address: indexed, outcome: int256: indexed, type: int256: indexed, orderID: address, price: int256, maker: address, taker:address, makerShares: int256, makerTokens: int256, takerShares: int256, takerTokens: int256, tradeGroupID: int256)

# Trade types
macro BID: 1
macro ASK: 2

def any():
    assertNoValue()

def init():
    self.owner = msg.sender

def initialize(controller: address):
    require(not self.initialized)
    self.initialized = 1
    self.controller = controller
    return 1

# @public
# @return fxp
def getAmount(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].fxpAmount)

# @public
# @return fxp
def getPrice(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].fxpPrice)

# @public
def getOrderOwner(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].owner)

# @public
# @return fxp
def getOrderSharesEscrowed(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].fxpSharesEscrowed)

# @public
# @return fxp
def getOrderMoneyEscrowed(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].fxpMoneyEscrowed)

# @public
# @return fxp
def getVolume(market: address):
    return(self.marketOrderData[market].volume)

# @public
# @return fxp
def getLastOutcomePrice(market: address, outcome: int256):
    return(self.marketOrderData[market].prices[outcome])

# @public
def getBetterOrderID(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].betterOrderID: address)

# @public
def getWorseOrderID(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].worseOrderID: address)

# @public
def getGasPrice(orderID: address, type: int256, market: address, outcome: int256):
    return(self.orders[market][outcome][type][orderID].gasPrice)

# @public
def getBestOrderID(type: int256, market: address, outcome: int256):
    return(self.bestOrder[market][outcome][type]: address)

# @public
def getWorstOrderID(type: int256, market: address, outcome: int256):
    return(self.worstOrder[market][outcome][type]: address)

# @public
def isBetterPrice(type: int256, market: address, outcome: int256, fxpPrice, orderID: address):
    if(type == BID):
        return((fxpPrice > self.orders[market][outcome][type][orderID].fxpPrice))
    else:
        return((fxpPrice < self.orders[market][outcome][type][orderID].fxpPrice))

# @public
def isWorsePrice(type: int256, market: address, outcome: int256, fxpPrice, orderID: address):
    if(type == BID):
        return((fxpPrice < self.orders[market][outcome][type][orderID].fxpPrice))
    else:
        return((fxpPrice > self.orders[market][outcome][type][orderID].fxpPrice))

# @public
def assertIsNotBetterPrice(type: int256, market: address, outcome: int256, fxpPrice, betterOrderID: address):
    require(not self.isBetterPrice(type, market, outcome, fxpPrice, betterOrderID))
    return(1)

# @public
def assertIsNotWorsePrice(type: int256, market: address, outcome: int256, fxpPrice, worseOrderID: address):
    require(not self.isWorsePrice(type, market, outcome, fxpPrice, worseOrderID))
    return(1)

# @internal
def insertOrderIntoList(orderID: address, type: int256, market: address, outcome: int256, fxpPrice: int256, betterOrderID: address, worseOrderID: address):
    self.controller.assertIsWhitelisted(msg.sender)
    bestOrderID = self.bestOrder[market][outcome][type]
    worstOrderID = self.worstOrder[market][outcome][type]
    boundingOrders = ORDERS_FETCHER.findBoundingOrders(type, market, outcome, fxpPrice, bestOrderID, worstOrderID, betterOrderID, worseOrderID, outitems=2)
    betterOrderID = boundingOrders[0]
    worseOrderID = boundingOrders[1]
    if(type == BID):
        bestOrderID = self.updateBestBidOrder(orderID, market, fxpPrice, outcome, betterOrderID)
        worstOrderID = self.updateWorstBidOrder(orderID, market, fxpPrice, outcome, worseOrderID)
    else:
        bestOrderID = self.updateBestAskOrder(orderID, market, fxpPrice, outcome, betterOrderID)
        worstOrderID = self.updateWorstAskOrder(orderID, market, fxpPrice, outcome, worseOrderID)
    if(bestOrderID == orderID):
        betterOrderID = 0
    if(worstOrderID == orderID):
        worseOrderID = 0
    if(betterOrderID != 0):
        self.orders[market][outcome][type][betterOrderID].worseOrderID = orderID
        self.orders[market][outcome][type][orderID].betterOrderID = betterOrderID
    if(worseOrderID != 0):
        self.orders[market][outcome][type][worseOrderID].betterOrderID = orderID
        self.orders[market][outcome][type][orderID].worseOrderID = worseOrderID
    return(1)

# @internal
def saveOrder(orderID: address, type: int256, market: address, fxpAmount: int256, fxpPrice: int256, sender: address, outcome: int256, fxpMoneyEscrowed: int256, fxpSharesEscrowed: int256, betterOrderID: address, worseOrderID: address, tradeGroupID: int256, gasPrice: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    require(type == BID or type == ASK)
    require(0 <= outcome and outcome < market.numOutcomes())
    self.insertOrderIntoList(orderID, type, market, outcome, fxpPrice, betterOrderID, worseOrderID)
    self.orders[market][outcome][type][orderID].fxpPrice = fxpPrice
    self.orders[market][outcome][type][orderID].fxpAmount = fxpAmount
    self.orders[market][outcome][type][orderID].owner = sender
    self.orders[market][outcome][type][orderID].fxpMoneyEscrowed = fxpMoneyEscrowed
    self.orders[market][outcome][type][orderID].fxpSharesEscrowed = fxpSharesEscrowed
    self.orders[market][outcome][type][orderID].gasPrice = gasPrice
    log(type=MakeOrder, market, sender, type, fxpPrice, fxpAmount, outcome, orderID, fxpMoneyEscrowed, fxpSharesEscrowed, tradeGroupID)
    return(1)

# @internal
def removeOrder(orderID: address, type: int256, market: address, outcome: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    require(tx.gasprice <= self.orders[market][outcome][type][orderID].gasPrice)
    self.removeOrderFromList(orderID, type, market, outcome)
    self.orders[market][outcome][type][orderID].fxpPrice = 0
    self.orders[market][outcome][type][orderID].fxpAmount = 0
    self.orders[market][outcome][type][orderID].owner = 0
    self.orders[market][outcome][type][orderID].fxpMoneyEscrowed = 0
    self.orders[market][outcome][type][orderID].fxpSharesEscrowed = 0
    self.orders[market][outcome][type][orderID].gasPrice = 0
    return(1)

# @internal
def fillOrder(orderID: address, orderType: int256, market: address, orderOutcome: int256, sharesFilled: int256, tokensFilled: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    require(market)
    require(0 <= orderOutcome and orderOutcome < market.numOutcomes())
    require(orderType == BID or orderType == ASK)
    require(orderID)
    require(sharesFilled <= self.orders[market][orderOutcome][orderType][orderID].fxpSharesEscrowed)
    require(tokensFilled <= self.orders[market][orderOutcome][orderType][orderID].fxpMoneyEscrowed)
    require(tx.gasprice <= self.orders[market][orderOutcome][orderType][orderID].gasPrice)
    require(self.orders[market][orderOutcome][orderType][orderID].fxpPrice <= market.maxDisplayPrice())
    require(self.orders[market][orderOutcome][orderType][orderID].fxpPrice >= market.minDisplayPrice())
    require(market.maxDisplayPrice() + market.minDisplayPrice() <= 2**254)
    fill = 0
    if orderType == BID:
        # We can't use safeSub here because it disallows subtracting negative numbers. Worst case here is an operation of 2**254 - 1 as required above, which won't overflow
        fill = sharesFilled + safeFxpDiv(tokensFilled, self.orders[market][orderOutcome][orderType][orderID].fxpPrice - market.minDisplayPrice())
    if orderType == ASK:
        # We can't use safeSub here because it disallows subtracting negative numbers. Worst case here is an operation of 2**254 - 1 as required above, which won't overflow
        fill = sharesFilled + safeFxpDiv(tokensFilled, market.maxDisplayPrice() - self.orders[market][orderOutcome][orderType][orderID].fxpPrice)
    require(fill <= self.orders[market][orderOutcome][orderType][orderID].fxpAmount)
    self.orders[market][orderOutcome][orderType][orderID].fxpAmount -= fill
    self.orders[market][orderOutcome][orderType][orderID].fxpMoneyEscrowed -= tokensFilled
    self.orders[market][orderOutcome][orderType][orderID].fxpSharesEscrowed -= sharesFilled
    if self.orders[market][orderOutcome][orderType][orderID].fxpAmount == 0:
        require(self.orders[market][orderOutcome][orderType][orderID].fxpMoneyEscrowed == 0)
        require(self.orders[market][orderOutcome][orderType][orderID].fxpSharesEscrowed == 0)
        self.removeOrderFromList(orderID, orderType, market, orderOutcome)
        self.orders[market][orderOutcome][orderType][orderID].fxpPrice = 0
        self.orders[market][orderOutcome][orderType][orderID].owner = 0
        self.orders[market][orderOutcome][orderType][orderID].gasPrice = 0
        self.orders[market][orderOutcome][orderType][orderID].betterOrderID = 0
        self.orders[market][orderOutcome][orderType][orderID].worseOrderID = 0
    return(1)

# @internal
def takeOrderLog(market: address, orderOutcome: int256, orderType: int256, orderID: address, taker: address, makerSharesFilled: int256, makerTokensFilled: int256, takerSharesFilled: int256, takerTokensFilled: int256, tradeGroupID: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    price = self.orders[market][orderOutcome][orderType][orderID].fxpPrice
    maker = self.orders[market][orderOutcome][orderType][orderID].owner
    log(type=TakeOrder, market, orderOutcome, orderType, orderID, price, maker, taker, makerSharesFilled, makerTokensFilled, takerSharesFilled, takerTokensFilled, tradeGroupID)
    return(1)

# @internal
def completeSetsLog(sender, market, type, fxpAmount, numOutcomes, marketCreatorFee, reportingFee):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=CompleteSets, sender, market, type, fxpAmount, numOutcomes, marketCreatorFee, reportingFee)
    return(1)

# @internal
def cancelOrderLog(market, sender, fxpPrice, fxpAmount, orderID, outcome, type, fxpMoneyEscrowed, fxpSharesEscrowed):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=CancelOrder, market, sender, fxpPrice, fxpAmount, orderID, outcome, type, fxpMoneyEscrowed, fxpSharesEscrowed)
    return(1: uint256)

# @internal
def modifyMarketVolume(market: address, fxpAmount: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    self.marketOrderData[market].volume += fxpAmount
    market.getBranch().getTopics().updatePopularity(market.topic(), fxpAmount)
    return(1)

# @internal
def setPrice(market: address, outcome: int256, fxpPrice: int256):
    self.controller.assertIsWhitelisted(msg.sender)
    self.marketOrderData[market].prices[outcome] = fxpPrice
    return(1: uint256)

# @private
def removeOrderFromList(orderID: address, type: int256, market: address, outcome: int256):
    assertPrivateCall()
    betterOrderID = self.orders[market][outcome][type][orderID].betterOrderID
    worseOrderID = self.orders[market][outcome][type][orderID].worseOrderID
    if(self.bestOrder[market][outcome][type] == orderID):
        self.bestOrder[market][outcome][type] = worseOrderID
    if(self.worstOrder[market][outcome][type] == orderID):
        self.worstOrder[market][outcome][type] = betterOrderID
    if(betterOrderID != 0):
        self.orders[market][outcome][type][betterOrderID].worseOrderID = worseOrderID
    if(worseOrderID != 0):
        self.orders[market][outcome][type][worseOrderID].betterOrderID = betterOrderID
    self.orders[market][outcome][type][orderID].betterOrderID = 0
    self.orders[market][outcome][type][orderID].worseOrderID = 0
    return(1)

# If best bid is not set or price higher than best bid price, this order is the new best bid.
# @private
def updateBestBidOrder(orderID: address, market: address, fxpPrice: int256, outcome: int256, betterOrderID: int256):
    assertPrivateCall()
    bestBidOrderID = self.bestOrder[market][outcome][BID]
    if(bestBidOrderID == 0 or fxpPrice > self.orders[market][outcome][BID][bestBidOrderID].fxpPrice):
        self.bestOrder[market][outcome][BID] = orderID
    return(self.bestOrder[market][outcome][BID]: address)

# If worst bid is not set or price lower than worst bid price, this order is the new worst bid.
# @private
def updateWorstBidOrder(orderID: address, market: address, fxpPrice: int256, outcome: int256, worseOrderID: int256):
    assertPrivateCall()
    worstBidOrderID = self.worstOrder[market][outcome][BID]
    if(worstBidOrderID == 0 or fxpPrice < self.orders[market][outcome][BID][worstBidOrderID].fxpPrice):
        self.worstOrder[market][outcome][BID] = orderID
    return(self.worstOrder[market][outcome][BID]: address)

# If best ask is not set or price lower than best ask price, this order is the new best ask.
# @private
def updateBestAskOrder(orderID: address, market: address, fxpPrice: int256, outcome: int256, betterOrderID: int256):
    assertPrivateCall()
    bestAskOrderID = self.bestOrder[market][outcome][ASK]
    if(bestAskOrderID == 0 or fxpPrice < self.orders[market][outcome][ASK][bestAskOrderID].fxpPrice):
        self.bestOrder[market][outcome][ASK] = orderID
    return(self.bestOrder[market][outcome][ASK]: address)

# If worst ask is not set or price higher than worst ask price, this order is the new worst ask.
# @private
def updateWorstAskOrder(orderID: address, market: address, fxpPrice: int256, outcome: int256, worseOrderID: int256):
    assertPrivateCall()
    worstAskOrderID = self.worstOrder[market][outcome][ASK]
    if(worstAskOrderID == 0 or fxpPrice > self.orders[market][outcome][ASK][worstAskOrderID].fxpPrice):
        self.worstOrder[market][outcome][ASK] = orderID
    return(self.worstOrder[market][outcome][ASK]: address)

# @controller
def setController(newController: address):
    require(msg.sender == self.controller)
    self.controller = newController
    return(1)

# @controller
def suicideFunds(to: address):
    require(msg.sender == self.controller)
    suicide(to)
